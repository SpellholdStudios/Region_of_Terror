IF
	StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_VOCALIZE)
END

IF
	See(NearestEnemyOf(Myself))
	Global("PrepSpells","LOCALS",0)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		ApplySpell(Myself,WIZARD_SPELL_TURNING)
		Wait(1)
		DisplayString(Myself,30942) // Contingency - Spell Immunity
		ApplySpell(Myself,WIZARD_SPELL_IMMUNITY_INVOCATION)
		SetGlobal("PrepSpells","LOCALS",1)
END

IF
	Allegiance(Myself,ENEMY)
	Global("PrepSpells","LOCALS",1)
	CheckStatLT(Myself,1,STONESKINS)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		DisplayString(Myself,29736) // Contingency - Stoneskin
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		SetGlobal("PrepSpells","LOCALS",2)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
	See(ThirdNearestEnemyOf(Myself))
	Global("Spell1","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpellRES("WAKEINF",ThirdNearestEnemyOf(Myself))           //WIZARD_WAKE_OF_THE_INFERNO
		SetGlobal("Spell1","LOCALS",1)
END

IF
	See(FourthNearestEnemyOf(Myself))
	Global("Spell2","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpellRES("WAKEINF",FourthNearestEnemyOf(Myself))          //WIZARD_WAKE_OF_THE_INFERNO
		SetGlobal("Spell2","LOCALS",1)
END

IF
	Allegiance(Myself,ENEMY)
	See(NearestEnemyOfType([0.0.0.LONG_BOW]))
	HaveSpell(WIZARD_RUBY_RAY_OF_REVERSAL)
	HaveSpell(WIZARD_MAZE)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_RUBY_RAY_OF_REVERSAL)
		Spell(LastSeenBy(Myself),WIZARD_MAZE)
END

IF
	Allegiance(Myself,ENEMY)
	See(NearestEnemyOfType([0.0.0.LONG_BOW]))
	HaveSpell(WIZARD_RUBY_RAY_OF_REVERSAL)
	HaveSpell(WIZARD_POWER_WORD_STUN)
	HPLT(LastSeenBy(Myself),90)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_RUBY_RAY_OF_REVERSAL)
		Spell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)
END

IF
	Global("PrepSpells","LOCALS",2)
	HaveSpell(WIZARD_STONE_SKIN)
	CheckStatLT(Myself,1,STONESKINS)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_STONE_SKIN)
END

IF
	HaveSpell(WIZARD_SPELL_DEFLECTION)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SPELL_DEFLECTION)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Spell3","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_MONSTER_SUMMONING_VI)
		SetGlobal("Spell3","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_PRISMATIC_SPRAY)
THEN
	RESPONSE #100
		Spell(ThirdNearestEnemyOf(Myself),WIZARD_PRISMATIC_SPRAY)
END

IF
	See(SecondNearestEnemyOf(Myself))
	HaveSpell(WIZARD_CHAIN_LIGHTNING)
	HaveSpell(WIZARD_PROTECTION_FROM_ELECTRICITY)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		DisplayString(Myself,31024) // Magic Tattoo - Mirror Image
		ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
		Spell(Myself,WIZARD_PROTECTION_FROM_ELECTRICITY)
		Spell(SecondNearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Spell4","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpell(LastSeenBy(Myself),WIZARD_SUMMON_HAKEASHAR)
		SetGlobal("Spell4","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_SHADOW_DOOR)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SHADOW_DOOR)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Spell6","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_PROTECTION_FROM_EVIL)
		SetGlobal("Spell6","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Spell5","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpell(LastSeenBy(Myself),WIZARD_SUMMON_FIEND)
		SetGlobal("Spell5","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Spell7","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_IMPROVED_MANTLE)
		SetGlobal("Spell7","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	!Range(ThirdNearestEnemyOf(Myself),12)
	HaveSpell(WIZARD_FIREBALL)
THEN
	RESPONSE #100
		Spell(ThirdNearestEnemyOf(Myself),WIZARD_FIREBALL)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_LIGHTNING_BOLT)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_LIGHTNING_BOLT)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_MELF_ACID_ARROW)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_MAGIC_MISSILE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(NearestEnemyOf(Myself),30)
END


